# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=teamspeak-management-tools
_basename=teamspeak-management-tools
pkgname=ksutils-$_basename
pkgver=1.0.1
pkgrel=1
pkgdesc='A teamspeak tools that help you manage your teamspeak server (include auto channel).'
arch=('any')
url=https://github.com/KunoiSayami/teamspeak-management-tools.rs
source=(
    $pkgname::git+https://github.com/KunoiSayami/teamspeak-management-tools.rs#tag=v$pkgver
    "$pkgname@.service"
    "default.toml"
)
license=('AGPL3')
sha512sums=(
    'SKIP'
    'edd4978a3b05b14d6352ca74e328c30af03e8665e786c4f8474df66bd9249d534a665891fdb9cf42e400f35a3887ec0cb95e10c7f32a1d2898fbd4b485aaf1b7'
    '7e6d378c49ed01bce2ecb92f6226204ce644fb379b6c6b2c82d951b45c898d0a59971dd198e4add35998918a20dfccad00f005dd4057e8fa15e7bdbf3ae53424'
)
makedepends=('cargo' 'git')
depends=('redis')
provides=("$_basename")
options=(!lto)
backup=("etc/ksutils/$_basename/default.toml")

prepare() {
    cd "$pkgname"
    git submodule update --init
}

build() {
    cd "$pkgname"
    cargo build --release
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname@.service" "$pkgdir"/usr/lib/systemd/system/
}
