# Maintainer: KunoiSayami <i@leanhe.dev>
# Maintainer: Stefan Auditor <stefan.auditor@erdfisch.de>
# Contributor: jsteel <mail at jsteel dot org>

_pkgname=php-ssh
pkgname=${_pkgname}-beta
pkgver=1.3.1
pkgrel=1
pkgdesc="An SSH2 extension for PHP"
url="http://pecl.php.net/package/ssh2"
license=('PHP')
arch=('i686' 'x86_64')
depends=('php>=7.0.0' 'openssh')
makedepends=('php>=7.0.0')
provides=('php-ssh')
conflicts=('php-ssh')
install=${_pkgname}.install
source=("${pkgname}::http://pecl.php.net/get/ssh2-1.3.1.tgz")
sha512sums=('c74d4f699853148b53cdca0eaf8837bfdbdd21d185761606acadf2442e84dbefdb00cde207e0ce4794ae1e4f8692a38ed903b6d11fdc57bca7b54a4250da9a76')
backup=("etc/php/conf.d/ssh2.ini")

build() {
  cd "${srcdir}/ssh2-${pkgver}"
  phpize
  ./configure --prefix=/usr --with-ssh2
  make -j $(nproc)
}

package() {
  cd "${srcdir}/ssh2-${pkgver}"
  echo ';extension=ssh2.so' > ssh2.ini
  install -Dm644 ssh2.ini "${pkgdir}/etc/php/conf.d/ssh2.ini"
  make install INSTALL_ROOT="${pkgdir}/"
}