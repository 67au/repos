# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=teamspeak-autochannel
_basename=teamspeak-autochannel
pkgname=ksutils-$_basename
pkgver=2.2.4
pkgrel=2
pkgdesc='TeamSpeak auto channel rust implement'
arch=('any')
url=https://github.com/KunoiSayami/teamspeak-autochannel.rs
source=(
    $pkgname::git+https://github.com/KunoiSayami/teamspeak-autochannel.rs#tag=v$pkgver
    "$pkgname@.service"
    "default.toml"
)
license=('AGPL3')
sha512sums=(
    'SKIP'
    'a8888343e3b9287a27804a011afba227352430071ae4e816055dc1f8ebba89d18a3150a3dda13774cd41865b6b1842df8ea6a76a05704b8de38d44dd37401430'
    '8b068b83fa0a996ccf461d0a8bab539b62dfc4fbfd8c0e9090f9dab8c5d44624780051f7c820cd566e306ec1a3322428f28f28c57779e657308495c0e397871f'
)
makedepends=('cargo' 'git')
depends=('redis')
provides=("$_basename")
options=(!lto)
backup=("etc/ksutils/$_basename/default.toml")

prepare() {
    cd "$pkgname"
    git submodule update --init
}

build() {
    cd "$pkgname"
    cargo build --release
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname@.service" "$pkgdir"/usr/lib/systemd/system/
}
