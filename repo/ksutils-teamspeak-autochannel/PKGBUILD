# Maintainer: KunoiSayami <i@leanhe.dev>

_original_name=teamspeak-autochannel
_basename=teamspeak-autochannel
pkgname=ksutils-$_basename
pkgver=0.4.0
pkgrel=1
pkgdesc='TeamSpeak auto channel rust implement'
arch=('any')
url=https://github.com/KunoiSayami/teamspeak-autochannel.rs
source=(
    $pkgname::git+https://github.com/KunoiSayami/teamspeak-autochannel.rs#tag=v$pkgver
    "$pkgname@.service"
    "default.toml"
)
license=('AGPL3')
sha512sums=(
    'SKIP'
    '26023b10bc410f439a317beb721a35bc1a133ce515117adbb96f489564c7d5a1afabc1bf5d6fa249178ec6031fe3374f7414f49d9d6622e60b6ef0bb4a98328a'
    'ff2659079989c3d53c7aaa5afacb22a5253ddb535a2e283598bc3152b6134c95adb60b253eb8112d9d735a64a3f962ec9dd26d288b98c3fdf3c01f9f4a6738cb'
)
makedepends=('cargo' 'git')
depends=('redis')
provides=("$_basename")
options=(!lto)

prepare() {
    cd "$pkgname"
    git submodule update --init
}

build() {
    cd "$pkgname"
    cargo build --release
}

package() {
    install -dm700 "$pkgdir"/etc/ksutils/$_basename
    install -dm755 "$pkgdir"/usr/bin
    install -dm755 "$pkgdir"/usr/lib/systemd/system

    install -m755 "$pkgname"/target/release/"$_original_name" "$pkgdir"/usr/bin/"$pkgname"
    install -m600 default.toml "$pkgdir"/etc/ksutils/$_basename/
    install -m644 "$pkgname@.service" "$pkgdir"/usr/lib/systemd/system/
}
