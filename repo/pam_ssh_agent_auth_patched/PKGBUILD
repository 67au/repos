# Maintainer: KunoiSayami <i@leanhe.dev>
pkgname=pam_ssh_agent_auth_patched
pkgver=0.10.4
pkgrel=3
pkgdesc="PAM module which permits authentication via the keyring in a forwarded ssh-agent."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/jbeverly/pam_ssh_agent_auth"
license=('custom:OpenSSL')
source=(pam_ssh_agent_auth_patched::git+https://github.com/FlorianFranzen/pam_ssh_agent_auth#commit=db2d41937e714b56d17771f8ee3d61f11c26cc7e)
md5sums=('SKIP')
makedepends=('git')
depends=('openssl')
optdepends=('openssh: standard ssh-agent'
            'gnupg: gpg ssh-agent')
provides=('pam_ssh_agent_auth')
conflicts=('pam_ssh_agent_auth')


prepare() {
  cd "$srcdir/$pkgname/ed25519-donna"
  git submodule init
  git submodule update
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --with-mantype=man --libexecdir=/usr/lib/security --without-openssl-header-check
  make -j $(nproc)
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE.OpenSSL "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
